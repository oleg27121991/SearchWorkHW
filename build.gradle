plugins {
    id 'java'
    id('io.qameta.allure') version '2.11.2'
}

group = 'by.veremei'
version = '1.0-SNAPSHOT'

def     slf4jVersion = "2.0.9",
        junitVersion = "5.10.1",
        javafakerVersion = "1.0.2",
        selenideVersion = "6.19.1",
        allureVersion = "2.24.0"

allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation(
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "com.codeborne:selenide:$selenideVersion",
            "com.github.javafaker:javafaker:$javafakerVersion",
            "io.qameta.allure:allure-selenide:$allureVersion")
    testRuntimeOnly (
            "org.slf4j:slf4j-simple:$slf4jVersion")
}

tasks.withType(Test) {
    systemProperties(System.getProperties())
    useJUnitPlatform()

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task all_test(type: Test) {
    useJUnitPlatform {
        includeTags("WEB_POSITIVE")
        includeTags("WEB_NEGATIVE")
    }
}

task negative_test(type: Test) {
    useJUnitPlatform {
        includeTags("WEB_NEGATIVE")
    }
}

task positive_test(type: Test) {
    useJUnitPlatform {
        includeTags("WEB_POSITIVE")
    }
}